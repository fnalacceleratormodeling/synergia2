! File fobodobo_s.lat
! 
! Written for the January, 2007 USPAS.
! To be used in conjunction with CHEF.
! 
! Send complaints to the author: Leo Michelotti
! 
! Add an RF cavity (EGS) 01/30/2009
!
! ------------------
! Parameters
! ------------------
twopi       :=   6.2831853072
n           :=   8                   !         : number of cells
momentum    := 100                   ! [GeV/c] : particle momentum; 
                                     !         :   arbitrary number
bendangle   := twopi/(2*n)           ! [rad]   : dipole bend angle
focus       :=   7                   ! [m]     : focal length of equivalent 
                                     !         :   thin quad
sepn        :=  10                   ! [m]     : distance between quad centers
quadlength  :=   0.2                 ! [m]     : quadrupole length
strength    := 1/(focus*quadlength)  ! [m**-2] : quadrupole strength
                                     !         :   = B'/brho, where
                                     !         :   brho = momentum/0.299792458
pct         :=   0.4                 !         : fraction of space between
                                     !         :   quads occupied by dipole
bendlength  := pct*(sepn-quadlength) ! [m]     : length of dipole
! bendlength := 0.4*(10-0.2) = 3.92
driftlength := (sepn-quadlength-bendlength)/2
! driftlenth := (10-0.2-3.92)/2 = 2.94
beam, particle=proton, pc=momentum
harmno = 80
lambda = 160.0/harmno
! resulting frequency = 1498896313.45363


hvoltage = 2000.0

! this lattice has 8 copies of a 20 m cell.

! ------------------
! Elements
! ------------------
o: drift,      l=driftlength
f: quadrupole, l=quadlength, k1=strength
d: quadrupole, l=quadlength, k1=(-strength)
b: sbend,      l=bendlength, angle=bendangle
r: rfcavity,	l=driftlength, volt=hvoltage, harmon=harmno

! ------------------
! Lattices
! ------------------
!fobodobo:  line=( f, o, b, o, d, o, b, o )
fobrdobo:  line=( f, o, b, r, d, o, b, o)
model:     line=( 8*fobrdobo )
! Doesn't work: model:     line=( n*fobodobo )

! File fobodobo_s.lat
! 
! Written for the January, 2007 USPAS.
! To be used in conjunction with CHEF.
! 
! Send complaints to the author: Leo Michelotti
! 
! Add an RF cavity (EGS) 01/30/2009
!
! ------------------
! Parameters
! ------------------
n           :=   32;                   !         : number of cells
bendangle   := twopi/(2*n);           ! [rad]   : dipole bend angle
focus       :=   7;                   ! [m]     : focal length of equivalent 
                                     !         :   thin quad
sepn        :=  10;                   ! [m]     : distance between quad centers
quadlength  :=   0.2;                 ! [m]     : quadrupole length
strength    := 1/(focus*quadlength);  ! [m**-2] : quadrupole strength
                                     !         :   = B'/brho, where
                                     !         :   brho = momentum/0.299792458
pct         :=   0.4;                 !         : fraction of space between
                                     !         :   quads occupied by dipole
bendlength  := pct*(sepn-quadlength); ! [m]     : length of dipole
! bendlength := 0.5*(10-2.0) = 4.0
driftlength := (sepn-quadlength-bendlength)/2;
! driftlenth := (10-2.0-4.0)/2 = 2.0
! harmonic number = 80  harmonic number, yields 2 meter wavelength
! the actual frequence is harmno following
harmno:=32;
lambda = (n*2*sepn)/harmno;

!hvoltage = 12500
hvoltage = 0.000001;

cavlen = 0.0; ! rf cavity length 1 meter, (half bucket length)
shortdlen = (driftlength - cavlen)/2.0;   ! 0.97 m
! this lattice has 32 copies of a 20 m cell.

! ------------------
! Elements
! ------------------

o: drift,      l=driftlength;
os: drift,      l=shortdlen;
f: quadrupole, l=quadlength, k1=strength;
d: quadrupole, l=quadlength, k1=(-strength);
b: sbend,      l=bendlength, angle=bendangle;
r: rfcavity,l=cavlen, volt=hvoltage, harmon=harmno, lag=0.0;

! chromaticity adjusters
sf: sextupole, l=0, k2=0.0;
sd: sextupole, l=0, k2=-0.0;


! ------------------
! Lattices
! ------------------
fobodobo:  line=( f, sf, o, b, o, d, sd, o, b, o );
fobrdobo:  line=( f, sf,  o, b, os, r, os, d, sd, o, b, o);
model:     line=( fobrdobo,31*fobodobo );
